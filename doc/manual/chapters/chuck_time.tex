\chapter{Time and Timing}

ChucK is a {\bf strongly-timed} language, meaning that time is fundamentally embedded in the language. ChucK allows the programmer to explicitly {\it reason} about time from the code. This gives extremely flexible and precise control over time and (therefore) sound synthesis.

In ChucK:

\begin{chuckitemize}
\item time and duration are native types in the language
\item keyword now holds the current logical time
\item time is {\it advanced} (and can only be advanced) by explicitly manipulating {\bf now}
\item you have flexible and precise control
\end{chuckitemize}


\section{time and duration}

Time and duration are native types in ChucK. {\bf time} represents an absolute point in time (from the beginning of ChucK time). {\bf dur} represents a duration (with the same logical units as {\bf time}).
\begin{verbatim}
    // a duration of one second
    1::second => dur foo;

    // a point in time (duration of foo from now)
    now + foo => time later;
\end{verbatim}
Later in this section, we outline the various arithmetic operations to perform on time and duration.

Durations can be used to construct new durations, which then be used to inductively construct yet other durations. For example:
\begin{verbatim}
    // .5 second is a quarter
    .5::second => dur quarter;

    // 4 quarters is whole
    4::quarter => dur whole;
\end{verbatim}

By default, ChucK provides these preset duration values:
\begin{chuckitemize}
\item {\bf samp} : duration of 1 sample in ChucK time
\item {\bf ms} : duration of 1 millisecond
\item {\bf second} : duration of 1 second
\item {\bf minute} : 1 minute
\item {\bf hour} : 1 hour
\item {\bf day} : 1 day
\item {\bf week} : 1 week
\end{chuckitemize}

Use these to represent any duration.

\begin{verbatim}
    // the duration of half a sample
    .5::samp => dur foo;

    // 20 weeks
    20::week => dur waithere;

    // use in combination
    2::minute + 30::second => dur bar;

    // same value as above
    2.5::minute => dur bar;
\end{verbatim}

\section{operations on time and duration (arithmetic)}

In ChucK, there are well-defined arithmetic operations on values of type {\bf time} and {\bf dur}.

example 1 (time offset):
\begin{verbatim}
    // time + dur yields time
    now + 10::second => time later;
\end{verbatim}

example 2 (time subtraction):
\begin{verbatim}
    // time - time yields dur
    later - now => dur D;
\end{verbatim}
example 3 (addition):
\begin{verbatim}
    // dur + dur yields dur
    10::second + 100::samp => dur foo;
\end{verbatim}
example 4 (subtraction):
\begin{verbatim}
    // dur - dur yields dur
    10::second - 100::samp => dur bar;
\end{verbatim}
example 5 (division):
\begin{verbatim}
    // dur / dur yields float
    10::second / 20::ms => float n;
\end{verbatim}
example 6 (time mod):
\begin{verbatim}
     // time mod dur yields dur
     now % 1::second => dur remainder;
\end{verbatim}
example 7 (synchronize to period):
\begin{verbatim}
    // synchronize to period of .5 second
    .5::second => dur T;
    T - (now % T) => now;
\end{verbatim}
example 8 (comparison on time):
\begin{verbatim}
    // compare time and time
    if( t1 < t2 )
        // do something...
\end{verbatim}
example 9 (comparison on duration):
\begin{verbatim}
    // compare dur and dur
    if( 900::ms < 1::second )
        <<< "yay!" >>>;
\end{verbatim}

\section{the keyword `now'}

The keyword {\bf now} is the key to reasoning about and controlling time in ChucK.

Some properties of {\bf now} include:
\begin{chuckitemize}
\item {\bf now} is a special variable of type time.
\item {\bf now} holds the current ChucK time (when read).
\item modifying {\bf now} has the side effects of:
	\item advancing time (see below);
	\item suspending the current process (called shred) until the desired time is reached - 	allowing other \item shreds and audio synthesis to compute;
\item the value of {\bf now} only changes when it is explicitly modified.
\end{chuckitemize}
(also see next section on advancing time).

Example:
\begin{verbatim}
    // compute value that represents "5 seconds from now"
    now + 5::second => time later;

    // while we are not at later yet...
    while( now < later )
    {
        // print out value of now
        <<< now >>>;

        // advance time by 1 second
        1::second => now;
    }
\end{verbatim}

\section{advancing time}

Advancing time allows other shreds (processes) to run and allows audio to be computed in a controlled manner. There are three ways of advancing time in ChucK:

\begin{chuckitemize}
\item chucking (\chuckop) a duration to {\bf now}: this will advance time by that duration.
\item chucking (\chuckop) a time to {\bf now}: this will advance time to that point. (note that the desired time must be later than the current time, or at least be equal to it.)
\item chucking (\chuckop) an {\bf Event to now}: time will advance until the event is triggered. (also see event)
\end{chuckitemize}

\subsection{advancing time by duration}
\begin{verbatim}
    // advance time by 1 second
    1::second => now;

    // advance time by 100 millisecond
    100::ms => now;

    // advance time by 1 samp (every sample)
    1::samp => now;

    // advance time by less than 1 samp
    .024::samp => now;
\end{verbatim}

\subsection{advancing time by absolute time}

\begin{verbatim}
    // figure out when
    now + 4::hour => time later;

    // advance time to later
    later => now;
\end{verbatim}

A time chucked to now will have ChucK wait until the appointed time. ChucK never misses an appointment (unless it crashes)! Again, the time chucked to now must be greater than or equal to now, otherwise an exception is thrown.

\subsection{advancing time by event}
\begin{verbatim}
    // wait on event
    e => now;
\end{verbatim}

See events for a more complete discussion of using events!

The advancement of time can occur at any point in the code.
\begin{verbatim}
    // our patch: sine oscillator -> dac
    SinOsc s => dac;

    // infinite time loop
    while( true )
    {
        // randomly choose frequency from 30 to 1000
        Std.rand2f( 30, 1000 ) => s.freq;

        // advance time by 100 millisecond
        100::ms => now;
    }
\end{verbatim}

Furthermore, there are no restrictions (other than underlying floating point precision) on how much time is advanced. So it is possible to advance time by a microsecond, a samp, 2 hours, or 10 years. The system will behave accordingly and deterministically.

This mechanism allows time to be controlled at any desired rate, according to any programmable pattern. With respect to sound synthesis, it is possible to control any unit generator at literally any rate, even sub-sample rate.

The power of the timing mechanism is extended by the ability to write parallel code, which is discussed in concurrency and shreds.

