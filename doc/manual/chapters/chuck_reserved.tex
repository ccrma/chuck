\chapter{Reserved Words}

\ugenheading{Control}

\ugen{for}\\
for loop - example:

\begin{verbatim}
        // print out now for the next 100 seconds, once a second
        for( 0 => int i; i < 100; i++ )
            1::second => now => stdout;
\end{verbatim}

\ugen{while}\\
while loop - example:
\begin{verbatim}
         // make time variable 'later', set to 5 seconds after now
        now + 5::second => time later;
   
        // while we are not there yet...
         while( now < later )
         {
            // print out the current ChucK time
            now => stdout;
            // advance time by 1 second
            1::second => now;
         }
\end{verbatim}

\ugen{until}\\
until loop - kind of the opposite of the while loop - example:
\begin{verbatim}
        // infinite time-loop
        until( false )
        {
           // print out now
           now => stdout;
           // advance time by 100 ms
           100::ms => now;
        }
\end{verbatim}

\ugen{if}\\
if - example:
\begin{verbatim}
        if( 1.0 > 0.0 ) "wow" => stdout;
\end{verbatim}

\ugen{else}\\
else - example:
\begin{verbatim}
        if( Std.rand2f( 0.0, 1.0 ) > .5 )
            "yes" => stdout;
        else
            "no" => stdout;
\end{verbatim}

\ugen{spork}
\begin{chuckitemize}
\item dynamically spork a shred, from a function or an expression
\item this operation is sample-synchronous, the new shred is shredule to execute immediate
\item in the current implementation, when a parent shred exits, all child shreds exit (this behavior will be enhanced in the future)
\item example (also see examples/: wind2.ck, pwm.ck, fmsynth.ck and others (\textgreater~ grep spork *.ck) in examples/:
\end{chuckitemize}

\begin{verbatim}
        // define function
        fun void foo( string s )
        {
             while( true )
             {
                 s => stdout;
                500::ms => now;
            }
        }
   
        // spork shred, passing in "you" as argument to foo
        spork ~ foo( "you" );
        // advance time by 250 ms
        250::ms => now;
        // spork another shred
        spork ~ foo( "me" );
   
        // infinite time loop - to keep child shreds around
        while( true )
             1::second => now;
\end{verbatim}

\ugen{return}\\
return from function - example:
\begin{verbatim}
        // define function
        fun int bar( int a, int b )
        {
            return a < b;
        }
\end{verbatim}

\ugen{(coming soon)}
\begin{chuckitemize}
\item switch
\item break
\item continue
\end{chuckitemize}


\ugenheading{special values}


\ugen{now}
\begin{chuckitemize}
\item special (global) value of type {\bf time}
\item when read as a value, {\bf now} holds current time in ChucK
\item when modified (by chucking a duration or time value to it), {\bf now} advances time for the current shred
\item {\bf now} never advances except when time is explicitly advanced in the program
\item because the above properties, {\bf now} is guaranteed to always hold the correct ChucK time
\end{chuckitemize}

example 1 (read):
\begin{verbatim}
        // assign now to a time value (note that foo will not advance with now)
        now => time foo;
\end{verbatim}

example 2 (read):
\begin{verbatim}
        // print the current time
        now => stdout;
\end{verbatim}

example 3 (write):
\begin{verbatim}
        // advance time by 500 ms
        500::ms => now;
\end{verbatim}

example 4 (both in one line):
\begin{verbatim}
        // first advance time, then print out now
        500::ms => now => stdout;
\end{verbatim}

example 5 (addition/subtraction - also see `arithmetic on time and duration')
\begin{verbatim}
        // later
        now + 10::second => time later;

        // subtract now from later (at this point, duration should equal exactly 10::second)
        later - now => dur duration;
\end{verbatim}

example 6 (comparison)
\begin{verbatim}
        // with later from example 5 defined
        while( now < later )
        {
            now => stdout;
            1::second => now;
        }
\end{verbatim}

\ugen{start}\\\
start time of the current shred

\ugen{NULL (or null)}\\
for objects

\ugen{true}\\
integer 1 - example:
\begin{verbatim}
        // infinite time-loop
        while( true )
            1::second => now;
\end{verbatim}

\ugen{false}\\
integer 0 - example:
\begin{verbatim}
        // infinite time-loop
        until( false )
            1::second => now;
\end{verbatim}

\ugen{maybe}\\
evaluates to 0 or 1, each with maybe (0.0-1.0) probability, maybe defaults to .5 - example:
\begin{verbatim}
        // for the non-decisive programmer
        if( maybe )
        {
            // do something
        }
\end{verbatim}

\ugen{function (or fun)}
used to define function - example:
\begin{verbatim}
        // define function
        fun int foo( int a )
        { return a+1; }
\end{verbatim}

\ugen{pi}\\
3.1415926... (and so on) (see also Math.pi and Math.e)

\ugen{(coming soon)}
\begin{chuckitemize}
\item class
\item extends
\item implements
\item public
\item protected
\item private
\item static
\item const
\item new
\end{chuckitemize}

\ugen{default types}
\begin{chuckitemize}
\item int: integer
\item float: floating point number
\item dur: duration
\item time: time
\item void: no type!
\item string: string
\item ugen: unit generator
\item stdout: output hack
\end{chuckitemize}



\ugen{(coming soon)}
\begin{chuckitemize}
\item object: like java.lang.Object, this is the class all non-primitives extend (directly or indirectly)
\end{chuckitemize}

\ugen{default durations (dur)}
\begin{chuckitemize}
\item samp: duration of 1 sample in ChucK time
\item ms: duration of 1 millisecond
\item second: duration of 1 second
\item minute: 1 minute
\item hour: 1 hour
\item day: 1 day
\item week: 1 week
\item (use these to inductively construct you own) - example:
\end{chuckitemize}
\begin{verbatim}
        // construct duration
        .5::second => dur foo;
        // use foo as part of another duration
        4::foo => dur bar;
\end{verbatim}

\ugen{arithmetic on time and duration}\\
in ChucK, there are well-defined arithmetic operations on values of type `time' and `dur'

example 1 (time offset):
\begin{verbatim}
        // offset time by duration
        now + 10::second => time later;
\end{verbatim}


example 2 (time subtraction):
\begin{verbatim}
        // time - time yields dur
        later - now => dur D;
\end{verbatim}

example 3 (addition):
\begin{verbatim}
        // dur + dur yields dur
        10::second + 100::samp => dur foo;
\end{verbatim}


example 4 (subtraction):
\begin{verbatim}
        // dur - dur yields dur
        10::second - 100::samp => dur bar;
\end{verbatim}


example 5 (division):
\begin{verbatim}
        // dur / dur yields number
        10::second / 20::ms => float n;
\end{verbatim}


example 6 (time mod):
\begin{verbatim}
        // time mod dur yields dur
        now % 1::second => dur remainder;
\end{verbatim}

example 7 (synchronize to period):
\begin{verbatim}
        // synchronize to period
        .5::second => dur T;
        T - (now % T) => now;
\end{verbatim}

the above is useful in precisely timing on-the-fly shreds (see otf\_0$[$1, 2, 3, 4, 5, 6, 7$]$.ck in examples/)

\ugen{operators}
\begin{chuckitemize}
\item +
\item -
\item *
\item /
\item \%
\item \chuckop
\item =\textless
\item \textbar \textbar
\item \&\&
\item ==
\item $\wedge$
\item \&
\item \textbar
\item \textasciitilde
\item ::
\item ++
\item --\,-- 
\item \textgreater
\item \textless
\item @=\textgreater
\item +=\textgreater
\item -=\textgreater
\item *=\textgreater
\item /=\textgreater
\item \%=\textgreater
\end{chuckitemize}

\ugen{reserved}
\begin{chuckitemize}
\item then
\item before
\item after
\item at
\end{chuckitemize}